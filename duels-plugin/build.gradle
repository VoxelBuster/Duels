import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
    id 'java'
    id "com.github.johnrengelman.shadow"
}

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDirectory = file("$rootDir/out/")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.22'
    runtimeOnly 'me.clip:placeholderapi:2.10.5'
    runtimeOnly('be.maximvdw:MVdWPlaceholderAPI:3.0.1-SNAPSHOT') { transitive = false }
    runtimeOnly 'com.SirBlobman.combatlogx:CombatLogX-API:10.0.0.0-SNAPSHOT'
    runtimeOnly name: 'Vault-1.6.7'
    runtimeOnly name: 'EssentialsX'
    runtimeOnly name: 'CombatTagPlus'
    runtimeOnly name: 'PvPManager-3.7.16'
    runtimeOnly name: 'Factions-1.6.9.5-U0.1.14'
    runtimeOnly name: 'MassiveCore'
    runtimeOnly name: 'Factions'
    runtimeOnly name: 'MyPet-2.3.4'
    runtimeOnly name: 'BountyHunters-2.2.6'
    runtimeOnly name: 'SimpleClans-2.14.4.1'
    runtimeOnly name: 'LeaderHeadsAPI'

    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation project(':duels-api')
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'org.inventivetalent.spiget-update:bukkit:1.4.3-SNAPSHOT'
    implementation 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    implementation 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'

}

shadowJar {
    archiveFileName = parent.name + '-' + project.version + '.jar'

    dependencies {
        include(dependency(':duels-worldguard'))
        include(dependency(':duels-worldguard-v6'))
        include(dependency(':duels-worldguard-v7'))
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
    relocate 'com.google.gson', group + 'gson'
}

build.dependsOn(shadowJar)