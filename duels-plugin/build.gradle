import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
    id 'java'
    id "com.github.johnrengelman.shadow"
}

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDirectory = file("$rootDir/out/")
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation project(':duels-api')
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'org.inventivetalent.spiget-update:bukkit:1.4.3-SNAPSHOT'

    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'

    compileOnly 'net.essentialsx:EssentialsX:2.19.0'
    compileOnly 'me.NoChance.PvPManager:PvPManager:3.8.10'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.github.sirblobman.combatlogx:api:11.0.0.0-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'be.maximvdw:MVdWPlaceholderAPI:3.1.1'
    compileOnly 'net.sacredlabyrinth.phaed.simpleclans:SimpleClans:2.16.0'
//    compileOnly 'com.github.Indyuce:bounty-hunters:master-SNAPSHOT'

/* likely completely borked for 1.18 but we will keep them anyway to try and maintain as much feature parity as possible
   may make easier in the future if the devs update their plugins.
   -- these plugins have no live repository to pull from*/
    compileOnly name: 'MassiveCore'
    compileOnly name: 'Factions'
    compileOnly name: 'Factions-1.6.9.5-U0.1.14'
    compileOnly name: 'BountyHunters-2.2.6'
    compileOnly name: 'SimpleClans-2.14.4.1'
    compileOnly name: 'LeaderHeadsAPI'

}

shadowJar {
    archiveFileName = parent.name + '-' + project.version + '.jar'

    dependencies {
        include(dependency(':duels-worldguard'))
        include(dependency(':duels-worldguard-v6'))
        include(dependency(':duels-worldguard-v7'))
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
    relocate 'com.google.gson', group + 'gson'
}

build.dependsOn(shadowJar)